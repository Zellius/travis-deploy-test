apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.jfrog.bintray'

group 'com.github.zellius'

def repository_url = 'https://github.com/Zellius/travis-deploy-test'
def lib_name = 'travis-deploy-test'
def lib_description = 'travis-deploy-test'
def lib_tag =  project.hasProperty('tag') ? project.property('tag') : System.getenv('TRAVIS_TAG')
def lib_version = ''

if(lib_tag != null){
    lib_version = (lib_tag =~ /v(\d+\.\d+\.\d+)/)[0][1]
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = lib_name
        desc = lib_description
        licenses = ['MIT']
        websiteUrl = repository_url
        issueTrackerUrl = "$repository_url/issues"
        vcsUrl = "${repository_url}.git"

        githubRepo = "Zellius/RxLocationManager"

        version {
            name = lib_version
            vcsTag = lib_tag
            released = new Date()
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier 'sources'
    //add lib sources and core sources
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    //archives androidJavadocsJar
}
